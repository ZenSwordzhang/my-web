plugins {
    id 'java'
    id 'org.springframework.boot' version '2.2.5.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.9.RELEASE' apply false
}

// 配置所有项目公共信息
allprojects {
    group 'com.zsx'
    version '1.0-SNAPSHOT'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    sourceCompatibility = 13
    targetCompatibility = 13
    ext {
        springCloudVersion = 'Hoxton.SR3'
    }
    repositories {
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'http://mvnrepository.com/' }
        maven { url 'https://repo1.maven.org/maven2/' }
        jcenter()
        mavenCentral()
        mavenLocal()
        // spring仓库
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://repo.spring.io/release" }
        maven { url 'http://repo.spring.io/plugins-snapshot' }
    }
    dependencies {
        // 使用springboot-web组件，但是排除tomcat组件
        compile('org.springframework.boot:spring-boot-starter-web') {
            exclude module:"spring-boot-starter-tomcat"
        }
        // 使用undertow作为应用服务,作为servlet容器
        compile 'org.springframework.boot:spring-boot-starter-undertow'
        testCompile('org.springframework.boot:spring-boot-starter-test')
        testCompile group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.6.1'
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.1'
    }
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
}
//配置子项目内容
subprojects {
    // 指定编码格式
    [compileJava,compileTestJava,javadoc]*.options*.encoding = 'UTF-8'
}
